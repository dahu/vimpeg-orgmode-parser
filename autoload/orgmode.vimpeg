; PEG grammar of simplified Emacs org-mode format
; Barry Arthur, 2014 06 07

.skip_white   = false
.namespace    = 'orgmodep'
.parser_name  = 'orgmode'
.root_element = 'orgline'

orgline          ::= ( directive | markup | content )* EOF          -> #orgline
directive        ::= ws '#' rest_of_line                            -> #directive
markup           ::= headline | properties
headline         ::= stars ws rest_of_line                          -> #headline
content_line     ::= !markup  rest_of_line                          -> #content_line
content          ::= ( content_line | nl )+
stars            ::= '\*\{1,3\}'
properties_begin ::= ws ':PROPERTIES:' nl
properties_end   ::= ws ':END:' nl
properties       ::= properties_begin property_pair* properties_end -> #properties
property_pair    ::= ws property_key ws property_value ws           -> #property_pair
property_key     ::= !properties_end ':[a-zA-Z_-]\+:'               -> #property_key
property_value   ::= rest_of_line
rest_of_line     ::= '.\{-}\ze\s*[\r\n]' nl                         -> #rest_of_line
ws               ::= '\s*'
nl               ::= ws '[\r\n]'
EOF              ::= !'.'
