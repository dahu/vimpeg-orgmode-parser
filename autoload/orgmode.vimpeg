; PEG grammar of simplified Emacs org-mode format
; Barry Arthur, 2014 06 07

.skip_white   = false
.namespace    = 'orgmodep'
.parser_name  = 'orgmode'
.root_element = 'orgline'

orgline          ::= ( directive | markup )* EOF                 -> #orgline
directive        ::= ws '#' ws directive_key ws rest_of_line     -> #directive
directive_key    ::= '+' non_prop_key                            -> #directive_key
non_prop_key     ::= '\C[A-Z]\+:'
markup           ::= headline | props | meta_data | content
content          ::= rest_of_line                                -> #content
headline         ::= stars ws rest_of_line                       -> #headline
meta_data        ::= (meta_key meta_val)+ nl                     -> #meta_data
meta_key         ::= ws non_prop_key ws                          -> #meta_key
meta_val         ::= meta_datetime | meta_date
meta_datetime    ::= '\[' '[^]]\+' '\]'                          -> #meta_datetime
meta_date        ::= '<' '[^>]\+' '>'                            -> #meta_date
stars            ::= '\*\{1,3\}'
prop_begin       ::= ws ':PROPERTIES:' nl
prop_end         ::= ws ':END:' nl
props            ::= prop_begin prop_pair* prop_end              -> #props
prop_pair        ::= ws prop_key ws prop_val ws                  -> #prop_pair
prop_key         ::= !prop_end ':[a-zA-Z_-]\+:'                  -> #prop_key
prop_val         ::= rest_of_line
rest_of_line     ::= '.\{-}\ze\s*[\r\n]' nl                      -> #rest_of_line
ws               ::= '\s*'
nl               ::= ws '[\r\n]'
EOF              ::= !'.'
